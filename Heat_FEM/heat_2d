load "iovtk"

real kappa = 1;
real dt = 0.01;

int n=32;
real  x0=0.0,y0=0.0,Lx=1.0,Ly=1.0;
border  a1(t=0,1){x=x0+Lx*t; y=y0;}
border  a2(t=0,1){x=x0+Lx; y=y0+Ly*t;}
border  a3(t=1,0){x=x0+Lx*t; y=y0+Ly;}
border  a4(t=1,0){x=x0; y=y0+Ly*t;}
mesh Th=buildmesh(a1(n)+a2(n)+a3(n)+a4(n));

plot ( Th, wait = 1, ps = "heat_mesh.ps" );

fespace Vh ( Th, P1 );

Vh uh, uold, vh, f, theta;
theta = 2*sin(pi*x)*(pi*y); //value at t=0
uh = theta;
uold = theta;

problem heat ( uh, vh, solver = LU ) = 
    int2d ( Th ) ( uh * vh / dt )
  - int2d ( Th ) ( uold * vh / dt )
  + int2d ( Th ) ( kappa * dx ( uh ) * dx ( vh )
                 + kappa * dy ( uh ) * dy ( vh ) )
  - int2d ( Th ) ( f * vh )
  + on (a1,a2,a3,a4, uh = 0 );

real t;
for ( int it = 1; it <= 1./dt; it++ )
  {
    t=it*dt;
    f = ( pi*cos(pi*t) + 2*kappa*pi*pi*(2+sin(pi*t)) ) * sin(pi*x) * sin(pi*y) ; //value of f
    heat;
    plot ( uh, nbiso = 25, fill = 1, value = 1, wait = 1, 
      ps = "heat_uh_" + it + ".ps" );
    uold = uh;
    savevtk("heat_plot"+it+".vtk",Th,uh,dataname="heat"); //saves vtk file for each iteration
  }


cout << "\n";
cout << "HEAT:\n";
cout << "  Normal end of execution.\n";

